buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        gradlePluginPortal()
    }
}

plugins {
    id "org.jetbrains.intellij" version "1.13.0"
    id "org.jetbrains.kotlin.jvm" version "1.8.10"
}

intellij {
    type = 'IU'
    version = '2022.3.2' // https://www.jetbrains.com/intellij-repository/releases com.jetbrains.intellij.idea
    plugins = ['org.intellij.intelliLang', 'terminal', 'PsiViewer:2022.3']
    pluginName = 'PowerShell'
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/gen'
        }
        resources {
            exclude '**.bnf'
            exclude '**.flex'
        }
    }
}

group 'com.intellij.plugin'
version '2.0.10-Omico'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.kohlschutter.junixsocket:junixsocket-common:2.3.3'
    implementation 'com.kohlschutter.junixsocket:junixsocket-native-common:2.3.3'

    implementation("org.eclipse.lsp4j:org.eclipse.lsp4j:0.3.0") {
        exclude group: 'com.google.code.gson', module: 'gson'
        exclude group: 'com.google.guava', module: 'guava'
    }
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
    testImplementation 'junit:junit:4.13.1'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.compileKotlin {
    kotlinOptions.jvmTarget = "17"
}

prepareSandbox {
    from("${project.rootDir}/language_host/current") {
        into "${intellij.pluginName.get()}/lib/"
    }
}
